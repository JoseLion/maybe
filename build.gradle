buildscript {
  configurations.classpath {
    resolutionStrategy.activateDependencyLocking()
  }
}

plugins {
  id('checkstyle')
  id('jacoco')
  id('java-library')
  id('maven-publish')
  id('signing')

  id('io.github.joselion.pretty-jupiter') version '3.0.1'
  id('io.github.joselion.strict-null-check') version '3.0.0'
  id('io.github.gradle-nexus.publish-plugin') version '2.0.0-rc-1'
  id('me.champeau.mrjar') version '0.1.1'
  id('name.remal.sonarlint') version '3.3.8'
}

group = 'io.github.joselion'

multiRelease {
  targetVersions(11, 17, 20)
}

java {
  toolchain {
    vendor = JvmVendorSpec.ORACLE
  }
  withJavadocJar()
  withSourcesJar()
}

javadoc {
  title = 'Maybe - Safely handle exceptions'
  options {
    addBooleanOption('html5', true)
    addStringOption('Xwerror', '-quiet')
    tags('apiNote')
  }
}

jar {
  from(sourceSets.main.allSource)
  manifest {
    attributes(
      'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
      'Build-Jdk-Spec': java.sourceCompatibility,
      'Build-Timestamp': LocalDateTime.now().toString(),
      'Created-By': "Gradle ${gradle.gradleVersion}",
      'Implementation-Title': project.name,
      'Implementation-Vendor': 'Jose Luis Leon',
      'Implementation-Version': project.version,
      'Package': "${project.group}.${project.name}",
    )
  }
}

sonarLint {
  setToolVersion('9.2.0.74516')
  languages {
    include('java')
  }
  rules {
    enable(
      'java:S4266', // "Stream.collect()" calls should not be redundant
    )
    disable(
      'java:S107', // Allow constructors with more than 7 parameters
      'java:S3776', // Allow methods with more than 15 lines
      'java:S4032', // Allow packages only containing `package-info.java`
    )
  }
}

strictNullCheck {
  packageInfo {
    useEclipse()
    javadoc = """\
      @author Jose Luis Leon
      @since v1.0.2
      """.stripIndent()
  }
  source.addEclipse()
}

checkstyle {
  setToolVersion('10.12.3')
}

dependencyLocking {
  lockAllConfigurations()
}

repositories {
  mavenCentral()
}

dependencies {
  sonarlintCorePlugins('org.sonarsource.java:sonar-java-plugin:7.24.0.32100')
  constraints {
    sonarlintCore('org.apache.tomcat.embed:tomcat-embed-core:9.0.80') {
      because('version 9.0.75 has a security vulnerability')
    }
  }
}

testing {
  suites {
    test {
      useJUnitJupiter('5.10.0')
      dependencies {
        implementation('org.assertj:assertj-core:3.24.2')
        implementation('org.mockito:mockito-core:5.5.0')
      }
    }
  }
}

java17Test.useJUnitPlatform()
java20Test.useJUnitPlatform()

prettyJupiter {
  duration.threshold = 150
}

jacoco {
  toolVersion('0.8.10')
}

jacocoTestReport {
  reports {
    xml.required = true
    html.required = true
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from(components.java)

      pom {
        name = 'maybe'
        packaging = 'jar'
        description = """\
          Maybe<T> is a monadic wrapper similar java.util.Optional, but with a different intention. \
          By leveraging Optional<T> benefits, it provides a functional API that safely allows us to \
          perform operations that may or may not throw checked and unchecked exceptions.

          The wrapper intends to help us avoid the imperative try/catch syntax, while promoting safe \
          exception handling and functional programming principles.
        """
        url = 'https://github.com/JoseLion/maybe'

        scm {
          connection = 'scm:git:https://github.com/JoseLion/maybe.git'
          developerConnection = 'scm:git:ssh://github.com/JoseLion/maybe.git'
          url = 'https://github.com/JoseLion/maybe'
        }

        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id = 'JoseLion'
            name = "Jose Luis Leon"
            email = 'joseluis5000l@gmail.com'
          }
        }
      }
    }
  }
}

signing {
  sign(publishing.publications.mavenJava)
}

nexusPublishing {
  repositories {
    sonatype {
      nexusUrl = uri('https://s01.oss.sonatype.org/service/local/')
      snapshotRepositoryUrl = uri('https://s01.oss.sonatype.org/content/repositories/snapshots/')
    }
  }
}
